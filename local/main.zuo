#lang zuo
(require "image.zuo"
         "compile.zuo")

(define config (cond
                 [(file-exists? "Makefile") (makefile->variables "Makefile")]
                 ;; no `configure`-generated `Makefile`, so use defaults
                 [(eq? (hash-ref (runtime-env) 'system-type) 'unix)
                  (hash 'srcdir "."
                        'INSTALL_PREFIX "/usr/local"
                        'CC "cc"
                        'CFLAGS "-O2")]
                 [else
                  (hash 'srcdir "."
                        'INSTALL_PREFIX "C:\\Program Files\\Zuo"
                        'CC "cl.exe"
                        'CFLAGS "/O2")]))

(define srcdir (hash-ref config 'srcdir))
(define install-prefix (hash-ref config 'INSTALL_PREFIX))

(define here (file-target (quote-module-path)))

(define image_zuo.c
  (let ([name (quote-path "image_zuo.c")])
    (target name
            (lambda (state)
              (embed-image (hash 'output name
                                 'deps "image_zuo.dep"
                                 'libs (map string->symbol (string-split (hash-ref config 'EMBED_LIBS "zuo")))
                                 'keep-collects? #t))
              (rule '() (file-timestamp name))))))

(define (exe-target name lib-path)
  (target name
          (lambda (state)
            (rule (list image_zuo.c)
                  (file-timestamp name)
                  (lambda (state)
                    (define l (split-path name))
                    (when (car l) (mkdir* (car l)))
                    (compile-c name
                               (target-path image_zuo.c state)
                               (list (~a "-DZUO_LIB_PATH=\"" lib-path "\""))
                               config
                               process-wait)
                    (file-timestamp name))))
          (hash 'noisy? #t)))

(define (add-exe name)
  (if (eq? (hash-ref (runtime-env) 'system-type) 'windows)
      (~a name ".exe")
      name))

(define zuo-to-run (exe-target (add-exe "to-run/zuo") (build-path srcdir ".." "lib")))
(define zuo-to-install (exe-target (add-exe "to-install/zuo") (~a install-prefix "/lib")))

(define zuos-to-run-and-install
  (target "zuos-to-run-and-install"
          (lambda (state)
            (rule (list zuo-to-run zuo-to-install)
                  'after
                  #f))))

(define install
  (target "install"
          (lambda (state)
            (rule (list zuo-to-run)
                  'before
                  (lambda (state)
                    (define (say-copy cp a b)
                      (displayln (~a "copying " a " to " b))
                      (cp a b))
                    (mkdir* install-prefix)
                    (mkdir* (build-path install-prefix "bin"))
                    (say-copy cp (target-path zuo-to-install state) (build-path install-prefix "bin" "zuo"))
                    (mkdir* (build-path install-prefix "lib"))
                    (say-copy cp*
                              (build-path srcdir "lib" "zuo")
                              (build-path install-prefix "lib" "zuo"))
                    'after)))))

(build-command-line (list zuos-to-run-and-install
                          image_zuo.c
                          zuo-to-run
                          zuo-to-install
                          install))
