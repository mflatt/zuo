#lang zuo
(require "image.zuo"
         "compile.zuo")

(define config (cond
                 [(file-exists? "Makefile") (config-file->hash "Makefile")]
                 ;; no `configure`-generated `Makefile`, so use defaults
                 [(eq? (hash-ref (runtime-env) 'system-type) 'unix)
                  (hash 'srcdir "."
                        'INSTALL_PREFIX "/usr/local"
                        'CC "cc"
                        'CFLAGS "-O2")]
                 [else
                  (hash 'srcdir "."
                        'INSTALL_PREFIX "C:\\Program Files\\Zuo"
                        'CC "cl.exe"
                        'CFLAGS "/O2")]))

(define srcdir (hash-ref config 'srcdir))
(define install-prefix (hash-ref config 'INSTALL_PREFIX))

(define (targets at-dir)
  (define image_zuo.c
    (image-target (hash 'output (at-dir "image_zuo.c")
                        'libs (map string->symbol (string-split (hash-ref config 'EMBED_LIBS "zuo")))
                        'keep-collects? #t)))

  (define (exe-target name lib-path)
    (target (at-dir name)
            (lambda (path token)
              (rule (list image_zuo.c
                          (input-data-target 'config config)
			  (quote-module-path)
			  (quote-path "compile.zuo"))
                    (file-sha1 path token)
                    (lambda ()
                      (define l (split-path path))
                      (when (car l) (mkdir* (car l)))
                      (compile-c path
                                 (target-path image_zuo.c)
                                 (list (~a "-DZUO_LIB_PATH=" lib-path))
                                 config
                                 thread-process-wait)
                      (file-sha1 path token))))))

  (define (add-exe name)
    (if (eq? (hash-ref (runtime-env) 'system-type) 'windows)
        (~a name ".exe")
        name))

  (define (as-c-string path) (~s path)) ; probably a good enough approximation

  (define zuo-to-run (exe-target (add-exe "to-run/zuo") (as-c-string (build-path srcdir ".." "lib"))))
  (define zuo-to-install (exe-target (add-exe "to-install/zuo") (as-c-string (build-path install-prefix "lib"))))
  
  (define zuos-to-run-and-install
    (target 'zuos-to-run-and-install
            (lambda (token)
              (phony-rule (list zuo-to-run zuo-to-install)
                          void))))

  (define install
    (target 'install
            (lambda (token)
              (phony-rule (list zuo-to-run)
                          (lambda ()
                            (define (say-copy cp a b)
                              (displayln (~a "copying " a " to " b))
                              (cp a b))
                            (mkdir* install-prefix)
                            (mkdir* (build-path install-prefix "bin"))
                            (say-copy cp (target-name zuo-to-install) (build-path install-prefix "bin" "zuo"))
                            (mkdir* (build-path install-prefix "lib"))
                            (say-copy cp*
                                      (build-path srcdir "lib" "zuo")
                                      (build-path install-prefix "lib" "zuo")))))))

  (list zuos-to-run-and-install
        image_zuo.c
        zuo-to-run
        zuo-to-install
        install))

(provide-targets targets)
