#lang zuo/private/macromod

(require "stx.zuo"
         "and-or.zuo"
         "syntax-error.zuo"
         "list.zuo"
         "let.zuo"
         "check-dups.zuo")

(provide (rename-out [define-var-or-proc define]))

(define-syntax define-var-or-proc
  (lambda (stx)
    (unless (and (stx-list? stx) (>= (stx-length stx) 3)) (bad-syntax stx))
    (let ([head (stx-cadr stx)])
      (cond
        [(identifier? head)
         ;; regular define
         (cons 'define (stx-cdr stx))]
        [(and (stx-pair? head)
              (identifier? (stx-car head)))
         ;; procedure shorthand
         (let* ([name (stx-car head)]
                [args (stx-cdr head)]
                [arg-names (let loop ([args args])
                             (cond
                               [(identifier? args) ; rest arg
                                (list args)]
                               [(stx-pair? args)
                                (unless (identifier? (car args))
                                  (syntax-error "define: bad argument" (car args)))
                                (cons (stx-car args) (loop (stx-cdr args)))]
                               [(null? args) '()]
                               [else (bad-syntax stx)]))])
           (check-duplicates arg-names)
           (list 'define name (list* 'lambda args (stx-cddr stx))))]
        [else (bad-syntax stx)]))))
